// Train to Hell
// ARCHITECTURE: AT0MIC
// SCRIPTING: AT0MIC & TLTRUDE

main:

// set scoreboard messages
setcvar "g_obj_alliedtext1" "Train to Hell"
setcvar "g_obj_alliedtext2" "by"
setcvar "g_obj_alliedtext3" "At0miC and Tltrude"
setcvar "g_obj_axistext1" ""
setcvar "g_obj_axistext2" ""
setcvar "g_obj_axistext3" ""

setcvar "g_scoreboardpic" "none"

local.master = spawn ScriptMaster
local.master aliascache train_eng sound/train/train1.wav soundparms 1 0.0 0.8 0.0 1500 6000 auto loaded maps "m dm moh obj "
local.master aliascache eng_loud sound/train/train1.wav soundparms 2.0 0.0 0.8 0.0 2500 6500 auto loaded maps "m dm moh obj "
local.master aliascache train_track2 sound/train/railclack2.wav soundparms 1.2 0.0 1.0 0.0 180 2200 auto loaded maps "m dm moh obj "
local.master aliascache track_loud sound/train/railclack2.wav soundparms 2.4 0.0 0.8 0.0 320 2500 auto loaded maps "m dm moh obj "
local.master aliascache train_bell sound/train/bells.wav soundparms 1.0 0.0 1.0 0.0 1024 6000 auto loaded maps "m dm moh obj "
local.master aliascache train_whistle sound/train/NorthernPacific.wav soundparms 1.0 0.0 1.0 0.0 1024 6000 auto loaded maps "m dm moh obj "
local.master aliascache railcar_open_snd sound/mechanics/m6l3a_SlidingDoors_Open.wav soundparms 1.0 0.0 1.0 0.0 160 1600 item loaded maps "m dm moh obj "
local.master aliascache railcar_close_snd sound/mechanics/m6l3a_SlidingDoors_Close.wav soundparms 1.0 0.0 1.0 0.0 160 1600 item loaded maps "m dm moh obj "
local.master aliascache train_creak1 sound/characters/fs_metalcreak_01.wav soundparms .8 0.4 0.7 0.6 160 800 auto loaded maps "m dm moh obj "
local.master aliascache train_creak2 sound/characters/fs_metalcreak_02.wav soundparms .8 0.4 0.7 0.6 160 800 auto loaded maps "m dm moh obj "
local.master aliascache train_creak3 sound/characters/fs_metalcreak_03.wav soundparms .8 0.4 0.7 0.6 160 800 auto loaded maps "m dm moh obj "
local.master aliascache train_creak4 sound/characters/fs_metalcreak_05.wav soundparms .8 0.4 0.7 0.6 160 800 auto loaded maps "m dm moh obj "
local.master aliascache train_creak5 sound/characters/fs_metalcreak_06.wav soundparms .8 0.4 0.7 0.6 160 800 auto loaded maps "m dm moh obj "
local.master aliascache crossing_bell sound/train/xing3.wav soundparms 1.0 0.0 1.0 0.0 1024 6000 auto loaded maps "m dm moh obj "
local.master aliascache smell_funny sound/dialogue/M6L3/A/dfr_M6L3_612h.wav soundparms 1.2 0.0 1.0 0.0 160 2200 auto loaded maps "m dm moh obj "
local.master aliascache car_move sound/vehicle/veh_truck_move.wav soundparms 1.5 0.0 1.0 0.0 800 4000 auto loaded maps "m dm moh obj "
local.master aliascache steamloop sound/mechanics/Mec_SteamLoop_01.wav soundparms 1.0 0.3 1.0 0.0 320 2200 item loaded maps "m dm moh obj "

	thread stack_smoke
	thread pole_prep
	thread start_sounds
	thread steam_start
	$track_ends time .001
	$track_ends moveto $wayA
	$track_ends move // moved for lighting

	$world farplane 9000 // was 18200
	$world farplane_color ".433 .445 .433" // was .6 .6 .65
	$world farplane_cull 0

	level waittill prespawn
 
	//***Precache Dm Stuff
	exec global/DMprecache.scr
	exec global/exploder.scr
	exec global/door_locked.scr::lock

	level.script = maps/dm/train_to_hell.scr
	exec global/ambient.scr m6l3a // for background sound
 
	$dor1_win bind $dor1 // breakable windows
	$dor2_win bind $dor2
	$dor3_win bind $dor3
	$dor4_win bind $dor4
	$knob1 bind $dor1 // non-solid knobs
	$knob2 bind $dor2
	$knob3 bind $dor3
	$knob4 bind $dor4

	$tree hide
	$tree2 hide
	$tree3 hide
	$sign hide
	$station_speaker bind $station
	$station hide
	$tunnel_hit_trigger bind $tunnel
	$tunnel_hit_trigger2 bind $tunnel
	$bridge_hit_trigger1 bind $bridge
	$bridge_hit_trigger2 bind $bridge
	$bridge_hit_trigger3 bind $bridge
	$bridge_hit_trigger4 bind $bridge
	$tunnel hide
	$crossing hide
	$water_tower hide
	$bridge hide
	$new_tunnel hide
	$new_tunnel bind $tunnel
	$riverwalls bind $bridge
	$riverwater bind $bridge	
	$riverwalls hide
	$riverwater hide
	$tunnel_shadow bind $tunnel

	local.car2 = spawn script_model model "vehicles/european_delivery_truck_green.tik" targetname "car_2"
	local.car2.origin = $car_1.origin
	local.car2.scale = 2.0

	local.car3 = spawn script_model model "vehicles/european_delivery_truck_red3.tik" targetname "car_3"
	local.car3.origin = ($car_1.origin + (0 800 0))
	local.car3.scale = 2.0
 
	$car_1 remove
	$car_2 hide
	$car_3 hide
	level.stationinview = 0
	level.tunnelinview = 0
	level.crossinview = 0
	level.towerinview = 0
	level.bridgeinview = 0
	level.pause_time = 1

	spawn info_waypoint targetname "polewayB2"
	$polewayB2.origin = ($polewayB.origin + ( -3800 0 0 ))
	spawn info_waypoint targetname "polewayA2"
	$polewayA2.origin = ($polewayA.origin + ( 3560 0 0 ))

	level waittill spawn 

	thread toot_whistle
	thread tunnelloop
	thread stationloop
	thread bridgeloop
	thread treeloop
	thread treeloop2
	thread treeloop3
	thread death_thread
	thread tunnel_death
	thread tunnel_death2
	thread bridge_death1
	thread bridge_death2
	thread bridge_death3
	thread bridge_death4
	thread signloop
	thread crossingloop
	thread towerloop
	thread car2loop
	thread car3loop
	thread train_shake
	thread servertext 
	thread tunnel_fade

end

//------------------------------>
// sound Threads
//------------------------------>

start_sounds:

	wait (level.pause_time)

	$track_speaker1 loopsound train_track2
	$track_speaker2 loopsound train_track2
	$track_speaker3 loopsound train_track2
	$track_speaker4 loopsound train_track2
	$track_speaker5 loopsound train_track2
	$track_speaker6 loopsound train_track2
	$track_speaker7 loopsound train_track2
	$track_speaker8 loopsound train_track2
	$engine_speaker loopsound train_eng

	while (level.tunnelinview !=1)
	waitframe
	waitthread stop_loop_snd
	$track_speaker1 loopsound track_loud
	$track_speaker2 loopsound track_loud
	$track_speaker3 loopsound track_loud
	$track_speaker4 loopsound track_loud
	$track_speaker5 loopsound track_loud
	$track_speaker6 loopsound track_loud
	$track_speaker7 loopsound track_loud
	$track_speaker8 loopsound track_loud
	$engine_speaker loopsound eng_loud
	wait 6
	waitthread stop_loop_snd
	goto start_sounds

end

stop_loop_snd:

	level.pause_time = 0
	$track_speaker1 stoploopsound
	$track_speaker2 stoploopsound 
	$track_speaker3 stoploopsound
	$track_speaker4 stoploopsound
	$track_speaker5 stoploopsound
	$track_speaker6 stoploopsound
	$track_speaker7 stoploopsound
	$track_speaker8 stoploopsound
	$engine_speaker stoploopsound
	exec global/earthquake.scr .8 .1 2 1 // extra shake for tunnel

end

toot_whistle:

	$toot_trigger waittill trigger
	$toot_trigger playsound train_whistle
	$toot_rope movedown 8
	$toot_rope move
	wait 1
	$toot_rope moveup 8
	$toot_rope waitmove
	thread toot_whistle

end

//------------------------------>
// tree Threads
//------------------------------> 

treeloop: 

	wait 1

	if (level.crossinview == 1)  // if crossing is in view, wait 2
	wait 2

	$tree time .001 
	$tree moveto $wayA  
	$tree waitmove

	$tree show
	$tree time 9.8333  
	$tree moveto $wayB 
	$tree waitmove
 
	$tree hide
	$tree time .001
	$tree moveto $wayC 
	$tree waitmove
 
	$tree time .001
	$tree moveto $wayD  
	$tree waitmove
 
	goto treeloop 

end

treeloop2:

	wait 3

	if (level.crossinview == 1)  // if crossing is in view, wait 2
	wait 2  

	$tree2 time .001 
	$tree2 moveto $wayA  
	$tree2 waitmove

	$tree2 show
	$tree2 time 9.8333  
	$tree2 moveto $wayB 
	$tree2 waitmove
 
	$tree2 hide
	$tree2 time .001
	$tree2 moveto $wayC 
	$tree2 waitmove
 
	$tree2 time .001
	$tree2 moveto $wayD  
	$tree2 waitmove

	goto treeloop2 

end

treeloop3:

	wait 2

	if (level.crossinview == 1)  // if crossing is in view, wait 2
	wait 2  

	$tree3 time .001 
	$tree3 moveto $wayA  
	$tree3 waitmove

	$tree3 show
	$tree3 time 9.8333  
	$tree3 moveto $wayB 
	$tree3 waitmove
 
	$tree3 hide
	$tree3 time .001
	$tree3 moveto $wayC 
	$tree3 waitmove
 
	$tree3 time .001
	$tree3 moveto $wayD  
	$tree3 waitmove

	goto treeloop3 

end

//------------------------------>
// Pole Thread
//------------------------------>

pole_prep: 

	local.poles = $pole1::$pole2::$pole3::$pole4::$pole5 

	local.poles[1].origin = $polewayA2.origin
	local.dist = 0.0

	//poles movement loop 

	while(1) 
	{  
	   for(local.i = 1; local.i <= local.poles.size; local.i++) 
	   { 
	      local.pole = local.poles[local.i] 
	      local.dist_on_track = local.dist + 3680 * (local.i - 1) 
	      if( local.dist_on_track > 18400 ) 
	      { 
	         local.dist_on_track = ( local.dist_on_track - 18400 ) 
	      } 
	      local.pole.origin = $polewayA2.origin + (local.dist_on_track * ( -1 0 0 ))
	   } 
	   waitframe 
	   local.dist = ( local.dist + 76.8 ) 
	   if( local.dist > 18400 ) 
	      local.dist = ( local.dist - 18400 )
	} 
  
end

//------------------------------>
// Station Thread
//------------------------------>
 
stationloop: 

	wait 300 // (5 minutes)

	while (level.tunnelinview == 1)  // while tunnel is in view, wait
	waitframe
	while (level.towerinview == 1)  // while tunnel is in view, wait
	waitframe

	$engine_speaker playsound train_whistle
	$station time .001 
	$station moveto $polewayA  
	$station waitmove

	$station show
	level.stationinview = 1
	$station_speaker loopsound train_bell
	$station time 7.18  
	$station moveto $polewayB 
	$station waitmove

	$station hide
	level.stationinview = 0
	$station_speaker stoploopsound
	$station time .001
	$station moveto $polewayC 
	$station waitmove
 
	$station time .001
	$station moveto $polewayD  
	$station waitmove
 
	goto stationloop

end

//------------------------------>
// Tunnel Threads
//------------------------------>

tunnelloop:

	wait 150 // 2.5 minutes

	while (level.stationinview == 1)  // while station is in view, wait
	waitframe
	while (level.towerinview == 1)  // while tower is in view, wait
	waitframe

	exec global/message_print.scr "               Tunnel!" NIL 6.9
	$tunnel time .001 
	$tunnel moveto $wayA  
	$tunnel waitmove

	$tunnel show
	$new_tunnel show
	$engine_speaker playsound train_whistle
	level.tunnelinview = 1

	$tunnel time 9.8333  
	$tunnel moveto $wayB 
	$tunnel waitmove
 
	$tunnel hide
	$new_tunnel hide
	level.tunnelinview = 0
	$tunnel time .001
	$tunnel moveto $wayC 
	$tunnel waitmove
 
	$tunnel time .001
	$tunnel moveto $wayD  
	$tunnel waitmove

	goto tunnelloop

end

tunnel_fade:

	$tunnel_shadow waittill trigger
	fadeout 2 0 0 0 1
	wait 2
	fadein 2 0 0 0 1

	goto tunnel_fade

end

//------------------------------>
// Bridge Thread
//------------------------------>

bridgeloop:

	wait 270 // 4.5 minutes

	while (level.stationinview == 1)  // while station is in view, wait
	waitframe
	while (level.towerinview == 1)  // while tower is in view, wait
	waitframe
	while (level.tunnelinview == 1)  // while tunnel is in view, wait
	waitframe

	exec global/message_print.scr "               Low bridge!" NIL 2
	//setcvar cg_3rd_person 1
	$bridge time .001 
	$bridge moveto $polewayA  
	$bridge waitmove

	$bridge show
	$riverwalls show
	$riverwater show
	$engine_speaker playsound train_whistle
	$bridge loopsound river
	level.bridgeinview = 1

	$bridge time 7.18  
	$bridge moveto $polewayB 
	$bridge waitmove
 
	//setcvar cg_3rd_person 0
	$bridge hide
	$riverwalls hide
	$riverwater hide
	$bridge stoploopsound
	level.bridgeinview = 0
	$bridge time .001
	$bridge moveto $polewayC 
	$bridge waitmove
 
	$bridge time .001
	$bridge moveto $polewayD  
	$bridge waitmove

	goto bridgeloop

end

//------------------------------>
// Sign Thread
//------------------------------>

signloop: 

	wait 90 // (1.5 minutes) 

	$sign time .001 
	$sign moveto $wayA  
	$sign waitmove

	$sign show
	$sign loopsound wind_trees3 4
	$sign time 9.8333  
	$sign moveto $wayB 
	$sign waitmove

	$sign hide
	$sign stoploopsound
	$sign time .001
	$sign moveto $wayC 
	$sign waitmove
 
	$sign time .001
	$sign moveto $wayD  
	$sign waitmove
 
	goto signloop

end

//------------------------------>
// Crossing Thread
//------------------------------>

crossingloop: 

	wait 200 // 3 minutes 20 seconds

	$engine_speaker playsound train_whistle
	$crossing time .001 
	$crossing moveto $wayA  
	$crossing waitmove

	$crossing show
	level.crossinview = 1
	$crossing loopsound crossing_bell
	$crossing time 9.8333  
	$crossing moveto $wayB 
	$crossing waitmove

	$crossing hide
	level.crossinview = 0
	$crossing stoploopsound
	$crossing time .001
	$crossing moveto $wayC 
	$crossing waitmove
 
	$crossing time .001
	$crossing moveto $wayD  
	$crossing waitmove
 
	goto crossingloop

end

//------------------------------>
// Water Tower Thread
//------------------------------>

towerloop: 

	wait 120  // 2 minutes

	while (level.tunnelinview == 1)  // while tunnel is in view, wait
	waitframe
	while (level.stationinview == 1)  // while station is in view, wait
	waitframe
	while (level.bridgeinview == 1)  // while bridge is in view, wait
	waitframe

	$water_tower time .001 
	$water_tower moveto $polewayA  
	$water_tower waitmove

	$water_tower show
	level.towerinview = 1
	$water_tower loopsound wind_trees3 4
	$water_tower time 7.18  
	$water_tower moveto $polewayB 
	$water_tower waitmove

	$water_tower hide
	level.towerinview = 0
	$water_tower stoploopsound
	$water_tower time .001
	$water_tower moveto $polewayC 
	$water_tower waitmove
 
	$water_tower time .001
	$water_tower moveto $polewayD  
	$water_tower waitmove
 
	goto towerloop

end

//------------------------------>
// Vehicle Threads
//------------------------------>

car2loop:

	wait (randomfloat 30 + 60) 

	$car_2 time .001 
	$car_2 moveto ($polewayA.origin  + ( 0 295 -248 ))
	$car_2 waitmove

	$car_2 show
	$car_2 loopsound car_move
	$car_2 time 18  
	$car_2 moveto ($polewayB.origin  + ( 0 295 -248 ))
	$car_2 waitmove

	$car_2 hide
	$car_2 stoploopsound
	$car_2 time .001
	$car_2 moveto $polewayC 
	$car_2 waitmove
 
	$car_2 time .001
	$car_2 moveto $polewayD  
	$car_2 waitmove
 
	goto car2loop

end

car3loop:

	wait (randomfloat 40 + 80) 

	$car_3 time .001 
	$car_3 moveto ($polewayA.origin  + ( 0 485 -240 ))
	$car_3 waitmove

	$car_3 show
	$car_3 loopsound car_move
	$car_3 time 20  
	$car_3 moveto ($polewayB.origin  + ( 0 485 -240 ))
	$car_3 waitmove

	$car_3 hide
	$car_3 stoploopsound
	$car_3 time .001
	$car_3 moveto $polewayC 
	$car_3 waitmove
 
	$car_3 time .001
	$car_3 moveto $polewayD  
	$car_3 waitmove
 
	goto car3loop

end

//------------------------------>
// Death Threads
//------------------------------>

death_thread:

	$deathfall_trigger waittill trigger
	local.player = parm.other
	radiusdamage local.player 500 16
	goto death_thread

end

tunnel_death:

	$tunnel_hit_trigger waittill trigger
	local.sucker = parm.other
	if (local.sucker istouching $tunnel_hit_trigger)
	{
		local.sucker playsound snd_bodyfall_stone5
		local.sucker damage local.sucker 1000 local.sucker (0 0 0) ( -300 0 128 ) (0 0 0) 300 0 rocket chest
		spawn script_model model
	}

end

tunnel_death2:

	$tunnel_hit_trigger2 waittill trigger
	local.sucker = parm.other
	if (local.sucker istouching $tunnel_hit_trigger2)
	{
		local.sucker playsound snd_bodyfall_stone5
		local.sucker damage local.sucker 1000 local.sucker (0 0 0) ( -300 0 128 ) (0 0 0) 300 0 rocket chest
		spawn script_model model
	}

end

bridge_death1:

	$bridge_hit_trigger1 waittill trigger
	local.player = parm.other
	local.player damage local.player 1000 local.player (0 0 0) (0 0 128) (0 0 0) 300 0 rocket chest
	$bridge playsound snd_bodyfall_metal3
	thread bridge_death1

end

bridge_death2:

	$bridge_hit_trigger2 waittill trigger
	local.player = parm.other
	local.player damage local.player 1000 local.player (0 0 0) (0 0 128) (0 0 0) 300 0 rocket chest
	$bridge playsound snd_bodyfall_metal3
	thread bridge_death2

end

bridge_death3:

	$bridge_hit_trigger3 waittill trigger
	local.player = parm.other
	local.player damage local.player 1000 local.player (0 0 0) (0 0 128) (0 0 0) 300 0 rocket chest
	$bridge playsound snd_bodyfall_metal3
	thread bridge_death3

end

bridge_death4:

	$bridge_hit_trigger4 waittill trigger
	local.player = parm.other
	local.player damage local.player 1000 local.player (0 0 0) (0 0 128) (0 0 0) 300 0 rocket chest
	$bridge playsound snd_bodyfall_metal3
	thread bridge_death4

end

//------------------------------>
// misc Threads
//------------------------------>

servertext:
  
  	wait 1 // 1 our
  	iprintlnbold_noloc " "
	$track_speaker8 playsound smell_funny
  
end

stack_smoke:

	local.smoke = spawn script_model model "fx/smokegood.tik"
	local.smoke.origin = $train_smoke_origin.origin + ( 0 0 16 )
	local.smoke.angle = 180
	local.smoke scale 1.5
	local.smoke anim start

end

steam_start:

	$steam_left anim start
	$steam_right anim start
	$steam_top anim start

end

train_shake:

	wait 12
	$track_speaker1 playsound train_creak
	$track_speaker2 playsound train_creak
	$track_speaker3 playsound train_creak
	$track_speaker4 playsound train_creak
	$track_speaker5 playsound train_creak
	$track_speaker6 playsound train_creak
	$track_speaker7 playsound train_creak
	$track_speaker8 playsound train_creak
	exec global/earthquake.scr .9 .1 2 1
	goto train_shake

end

kaboom_crates:

	exec global/exploder.scr::explode 1

end

kaboom_crates2:

	exec global/exploder.scr::explode 2

end

// 2004 - July/October